model{
	for(i in 1:Nsub) {
		Y[i] ~ dnorm(m[i],t[i])
		S[i] ~ dcat(p[])
		m[i]<- xi[S[i]]
		t[i]<- tau[S[i]]
		y.rep[i] ~ dnorm(m[i],t[i])
		Z[i]<- ifelse(S[i]>=2,1,0)

# Discrepancy measures between observed and replicated data		
	  r[i]<-Y[i]-m[i] 
	  sr[i]<-r[i]/sqrt(t[i]) 
	  r.rep[i]<-y.rep[i]-m[i]
	  sr.rep[i]<-r.rep[i]/sqrt(t[i])
	  m2[i]<-pow(sr[i],2) 
	  m2.rep[i]<-pow(sr.rep[i],2)
	}

# Estimating mixture parameters
	for(j in 1:K){
		#xi0[j] ~ dunif(0,10)
		xi0[j] ~ dnorm(eta[j],zeta[j])T(0,)
		psi[j] ~ dnorm(0,0.01)
    #sigma[j] ~ dt(0,0.01,1)T(0,) # half-cauchy with large scale on std. dev.
    #sigma[j] ~ dunif(0,100)      # uniform with large on std. dev.
    #tau[j]<-1/pow(sigma[j],2)    
    tau[j] ~ dgamma(0.01,0.01)    # gamma with large range on precision
    sigma[j]<-1/sqrt(tau[j])
		eta[j] ~ dnorm(0,0.001)		
		zeta[j] ~ dgamma(0.001,0.001)
	}
	xi[1:K]<-sort(xi0)

# Mean squared error for in-sample predictions
#	mse.y<-sum(r2[])/(Nsub)
  
# Overall seroprevalence
  p[1:K] ~ ddirch(e0[])

# Posterior predictive p-value
	Tstat<-mean(m2[])
	Tstat.rep<-mean(m2.rep[])
	ppp<-step(Tstat.rep-Tstat)
}
